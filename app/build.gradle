/*
 * Copyright 2015 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    dependencies {
        //classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

apply plugin: 'com.android.application'

// query git for the SHA, Tag and commit count. Use these to automate versioning.
def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
def gitCommitCount = 100 +
        Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "io.plaidapp"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode gitCommitCount
        versionName gitTag
        archivesBaseName = "plaid"
        resConfig "en"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "DRIBBBLE_CLIENT_ID", "\"${dribbble_client_id}\""
        buildConfigField "String", "DRIBBBLE_CLIENT_SECRET", "\"${dribbble_client_secret}\""
        buildConfigField "String",
                "DRIBBBLE_CLIENT_ACCESS_TOKEN", "\"${dribbble_client_access_token}\""
        buildConfigField "String", "DESIGNER_NEWS_CLIENT_ID", "\"${designer_news_client_id}\""
        buildConfigField "String",
                "DESIGNER_NEWS_CLIENT_SECRET", "\"${designer_news_client_secret}\""
        buildConfigField "String",
                "PROCUCT_HUNT_DEVELOPER_TOKEN", "\"${product_hunt_developer_token}\""
        def filesAuthorityValue = applicationId + ".shareprovider";
        buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    google()
    jcenter()
}

ext.supportLibVersion = '28.0.0'
ext.retrofit2Version = '2.2.0'

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:customtabs:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.android.support:palette-v7:${supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation project(':glide-pro')
    //implementation 'com.github.yongjhih.android-proguards:glide-pro:-SNAPSHOT'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation project(':gson-pro')
    //implementation 'com.github.yongjhih.android-proguards:gson-pro:-SNAPSHOT'
    implementation 'com.jakewharton:butterknife:8.4.0'
    // butterknife8: DONT need proguard anymore, because it's already consumed
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    implementation "com.squareup.retrofit2:retrofit:${retrofit2Version}"
    implementation project(':retrofit2-pro')
    //implementation 'com.github.yongjhih.android-proguards:retrofit2-pro:-SNAPSHOT'
    implementation "com.squareup.retrofit2:converter-gson:${retrofit2Version}"
    implementation 'com.squareup.okhttp3:okhttp:3.6.0'
    implementation project(':okhttp3-pro')
    //implementation 'com.github.yongjhih.android-proguards:okhttp3-pro:-SNAPSHOT'
    implementation 'org.jsoup:jsoup:1.10.1'
    implementation project(':jsoup-pro')
    //implementation 'com.github.yongjhih.android-proguards:jsoup-pro:-SNAPSHOT'
    implementation project(':bypass')
}
